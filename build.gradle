plugins {
    id "groovy"
    id "com.github.johnrengelman.shadow" version "6.1.0" apply false
    id "io.micronaut.library" version "1.4.2" apply false
    id "io.micronaut.application" version "1.4.2" apply false
    id "com.diffplug.spotless" version "5.12.4" apply false
}

subprojects { subproject ->

    version "0.1"
    group "ch.onstructive.candidates"

    if (subproject.name.endsWith("server")) {
        apply plugin: "io.micronaut.application"
    }
    else {
        apply plugin: "io.micronaut.library"
    }
    apply plugin: 'groovy'
    apply plugin: "com.diffplug.spotless"

    micronaut {
        runtime("netty")
        testRuntime("spock2")
        processing {
            incremental(true)
            annotations("com.example.*")
        }
    }

    dependencies {
        annotationProcessor("io.micronaut.data:micronaut-data-processor")
        implementation("io.micronaut:micronaut-http-client")
        implementation("io.micronaut:micronaut-runtime")
        implementation("io.micronaut.data:micronaut-data-hibernate-jpa")
        implementation("io.micronaut.flyway:micronaut-flyway")
        implementation("io.micronaut.sql:micronaut-jdbc-hikari")
        implementation("javax.annotation:javax.annotation-api")
        implementation("io.micronaut:micronaut-validation")
        runtimeOnly("ch.qos.logback:logback-classic")
        runtimeOnly("com.h2database:h2")

        implementation "org.mapstruct:mapstruct:${mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    }

    java {
        sourceCompatibility = JavaVersion.toVersion('15')
        targetCompatibility = JavaVersion.toVersion('15')
    }

    spotless {
        java {
            googleJavaFormat()
            licenseHeader '/* Licensed under Apache-2.0 */'

            importOrder 'ch', 'java', 'javax', 'org', 'com', 'com.diffplug', ''    // A sequence of package names
            removeUnusedImports() // removes any unused imports
        }
        groovy {
            licenseHeader '/* Licensed under Apache-2.0 */'
            excludeJava()
            greclipse()
        }
    }
}












